#
#  configure.in - hplip autoconf input file
# 
#  (c) 2004-2007 Copyright Hewlett-Packard Development Company, LP
# 
# exit status:
#   0 = ok
#   1 = error
#   2 = no libusb
#   3 = no cups-devel
#   4 = no libnetsnmp
#   5 = no netsnmp-devel
#   6 = no python-devel
#   7 = no pthread-devel
#   8 = no ppdev-devel
#   9 = no libcups
#   10 = no libm
#   11 = no libusb-devel
#   12 = no sane-backends-devel
#   102 = no libjpeg
#   103 = no jpeg-devel
#   104 = no libdl
#

AC_PREREQ(2.59)
AC_INIT([HP Linux Imaging and Printing], [2.7.9], [2.7.9.13], [hplip])
AM_INIT_AUTOMAKE([1.9 foreign])
AC_DISABLE_STATIC
AC_PROG_LIBTOOL

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_INSTALL

# Checks for required libraries, sets -lpthread, -lm, -ljpeg, ... for LIBS and HAVE_PTHREAD, ...
AC_CHECK_LIB([pthread], [pthread_create])
AC_CHECK_LIB([m], [pow],, [AC_MSG_ERROR([cannot find libm math support], 10)])
AC_CHECK_LIB([jpeg], [jpeg_set_defaults],, [AC_MSG_ERROR(["cannot find libjpeg support"], 102)])
AC_CHECK_LIB([dl], [dlopen],, [AC_MSG_ERROR(["cannot find libdl support"], 104)])

# Checks for required header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(pthread.h,, [AC_MSG_ERROR([cannot find pthread-devel support], 7)])
AC_CHECK_HEADERS(linux/compiler.h)
AC_CHECK_HEADERS(fcntl.h malloc.h syslog.h unistd.h)
AC_CHECK_HEADERS(jpeglib.h,, [AC_MSG_ERROR([cannot find libjpeg-devel support], 103)])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_C_BIGENDIAN(,[APDK_ENDIAN_FLAG="-DAPDK_LITTLE_ENDIAN"])

# Autoconf-style header tests for APDK
cat >prnt/hpijs/auto-include.h <<EOFH
#ifdef HAVE_INTTYPES_H
#include <inttypes.h>
#endif
#ifdef HAVE_STDINT_H
#include <stdint.h>
#endif
#ifdef HAVE_MACHINE_TYPES_H
#include <machine/types.h>
#endif
EOFH
AC_CHECK_TYPES([uint32_t], [APDK_AUTO_INCLUDE_FLAG="-DAPDK_AUTO_INCLUDE"],, [#include "prnt/hpijs/auto-include.h"])

AC_MSG_CHECKING("for platform-dependencies")
darwin_build="no"
case "$host" in
   *-darwin*)
      AC_MSG_RESULT("using Mac OS X platform.h")
      cat >prnt/hpijs/platform.h <<EOF
#include <stdlib.h>
#include <sys/types.h>
#include <sys/malloc.h>
#include <memory.h>
#include <string.h>
#include <stdio.h>
#include <math.h>
EOF
      darwin_build="yes"
      ;;

     *)
      AC_MSG_RESULT("using Default platform.h")
      cat >prnt/hpijs/platform.h <<EOF
#include <stdlib.h>
#include <memory.h>
#include <string.h>
#include <stdio.h>
#include <math.h>
EOF
      ;;
esac
AM_CONDITIONAL(DARWIN_BUILD, test x$darwin_build = xyes)

AC_MSG_CHECKING([for documentation build])
AC_ARG_ENABLE(doc_build,
  [  --enable-doc-build     enable documentation build (default=yes)],
  doc_build=$enableval, doc_build=yes)
if test "$doc_build" = "yes"; then
   AC_MSG_RESULT(yes)
else
   AC_MSG_RESULT(no)
fi
AM_CONDITIONAL(DOC_BUILD, test x$doc_build = xyes)

AC_MSG_CHECKING([for hpijs only build])
AC_ARG_ENABLE(hpijs_only_build,
  [  --enable-hpijs-only-build     enable hpijs only build (default=no)],
  hpijs_only_build=$enableval, hpijs_only_build=no)
if test "$hpijs_only_build" = "yes"; then
   AC_MSG_RESULT(yes)
else
   AC_MSG_RESULT(no)
   AC_DEFINE(HAVE_LIBHPIP) 
fi
AM_CONDITIONAL(HPLIP_BUILD, test x$hpijs_only_build = xno)

AC_MSG_CHECKING([for network build])
AC_ARG_ENABLE(network_build,
  [  --enable-network-build    enable network build (default=yes)],
  network_build=$enableval, network_build=yes)
if test "$network_build" = "yes"; then
   AC_MSG_RESULT(yes)
else
   AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING([for parallel port build])
AC_ARG_ENABLE(pp_build,
  [  --enable-pp-build    enable parallel port build (default=yes)],
  pp_build=$enableval, pp_build=yes)
if test "$pp_build" = "yes"; then
   AC_MSG_RESULT(yes)
   AC_DEFINE(HAVE_PPORT) 
else
   AC_MSG_RESULT(no)
fi
   
AC_MSG_CHECKING([for scanner build])
AC_ARG_ENABLE(scan_build,
  [  --enable-scan-build    enable scanner build (default=yes)],
  scan_build=$enableval, scan_build=yes)
if test "$scan_build" = "yes"; then
   AC_MSG_RESULT(yes)
else
   AC_MSG_RESULT(no)
fi
AM_CONDITIONAL(SCAN_BUILD, test x$scan_build = xyes)

AC_MSG_CHECKING([for gui build])
AC_ARG_ENABLE(gui_build,
  [  --enable-gui-build    enable gui build (default=yes)],
  gui_build=$enableval, gui_build=yes)
if test "$gui_build" = "yes"; then
   AC_MSG_RESULT(yes)
else
   AC_MSG_RESULT(no)
fi
AM_CONDITIONAL(GUI_BUILD, test x$gui_build = xyes)

AC_MSG_CHECKING([for fax build])
AC_ARG_ENABLE(fax_build,
  [  --enable-fax-build    enable fax build (default=yes)],
  fax_build=$enableval, fax_build=yes)
if test "$fax_build" = "yes"; then
   AC_MSG_RESULT(yes)
else
   AC_MSG_RESULT(no)
fi
AM_CONDITIONAL(FAX_BUILD, test x$fax_build = xyes)

AC_MSG_CHECKING([for cups 1.1.x build])
AC_ARG_ENABLE(cups11_build,
  [  --enable-cups11-build    enable cups 1.1.x build (default=no)],
  cups11_build=$enableval, cups11_build=no)
if test "$cups11_build" = "yes"; then
   AC_MSG_RESULT(yes)
   AC_DEFINE(HAVE_CUPS11) 
else
   AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING([for shadow build])
AC_ARG_ENABLE(shadow_build,
  [  --enable-shadow-build    enable shadow build (default=no)],
  shadow_build=$enableval, shadow_build=no)
if test "$shadow_build" = "yes"; then
   AC_MSG_RESULT(yes)
else
   AC_MSG_RESULT(no)
fi
AM_CONDITIONAL(SHADOW_BUILD, test x$shadow_build = xyes)

AC_ARG_WITH(cupsbackenddir, AC_HELP_STRING([--with-cupsbackenddir=DIR], [set cups backend install directory [default=/usr/lib/cups/backend]]),
   cupsbackenddir=$withval, cupsbackenddir="/usr/lib/cups/backend")

AC_ARG_WITH(icondir, AC_HELP_STRING([--with-icondir=DIR], [set icon install directory [default=/usr/share/applications]]),
   icondir=$withval, icondir="/usr/share/applications")

AC_ARG_WITH(hpppddir, AC_HELP_STRING([--with-hpppddir=DIR], [set hp ppd install directory [default=datadir/ppd/HP]]),
   hpppddir=$withval, hpppddir="$datadir/ppd/HP")

AC_ARG_WITH(docdir, AC_HELP_STRING([--with-docdir=DIR], [set hplip documentation directory [default=datadir/doc]]),
   hpdocdir=$withval, hpdocdir="$datadir/doc/hplip-$VERSION")

AC_MSG_CHECKING("for foomatic ppd install")
AC_ARG_ENABLE(foomatic_ppd_install,
  [  --enable-foomatic-ppd-install         enable foomatic ppd install, uses hpppddir [default=no]],
  foomatic_ppd_install=$enableval, foomatic_ppd_install=no)
if test $foomatic_ppd_install = yes; then
   AC_MSG_RESULT(yes)
else
   AC_MSG_RESULT(no)
fi

AC_ARG_WITH(foomaticdir, AC_HELP_STRING([--with-foomaticdir=DIR], [set foomatic db install directory [default=datadir/foomatic]]),
   foomaticdir=$withval, foomaticdir="$datadir/foomatic")

AC_MSG_CHECKING("for foomatic xml install")
AC_ARG_ENABLE(foomatic_xml_install,
  [  --enable-foomatic-xml-install         enable foomatic xml install, uses foomaticdir [default=yes]],
  foomatic_xml_install=$enableval, foomatic_xml_install=yes)
if test $foomatic_xml_install = yes; then
   AC_MSG_RESULT(yes)
else
   AC_MSG_RESULT(no)
fi

# Check conditional packages.

if test "$hpijs_only_build" = "no" && test "$network_build" = "yes"; then
   AC_CHECK_LIB([crypto], [CRYPTO_free])
   AC_CHECK_LIB([netsnmp], [snmp_timeout],, [AC_MSG_ERROR([cannot find net-snmp support (or --disable-network-build)], 4)])
   AC_CHECK_HEADERS(net-snmp/net-snmp-config.h,, [AC_MSG_ERROR([cannot find net-snmp-devel support (or --disable-network-build)], 5)])
fi

if test "$hpijs_only_build" = "no" && test "$pp_build" = "yes"; then
   AC_CHECK_HEADERS(linux/ppdev.h, ,[AC_MSG_ERROR([cannot find ppdev-devel support (or --disable-pp-build)], 8)])
fi

if test "$hpijs_only_build" = "no"; then
   AC_CHECK_LIB([cups], [cupsDoFileRequest],, [AC_MSG_ERROR([cannot find libcups support], 9)])
   AC_CHECK_HEADERS(cups/cups.h, ,[AC_MSG_ERROR([cannot find cups-devel support], 3)])
   AC_CHECK_LIB([usb], [usb_init],, [AC_MSG_ERROR([cannot find libusb support], 2)])
   AC_CHECK_HEADERS(usb.h, ,[AC_MSG_ERROR([cannot find libusb-devel support], 11)])
   AC_ARG_VAR([PYTHON], [Python interpreter/compiler command])
   AM_PATH_PYTHON([2.2])
   AC_MSG_CHECKING([for path to Python.h])
   PYTHONINCLUDEDIR=`$PYTHON -c "from distutils.sysconfig import get_python_inc; print get_python_inc();"`
   AC_MSG_RESULT("using $PYTHONINCLUDEDIR")
   AC_ARG_VAR(PYTHONINCLUDEDIR, [path to Python.h C header file])
   AC_CHECK_HEADERS(python$PYTHON_VERSION/Python.h, ,[AC_MSG_ERROR([cannot find python-devel support], 6)])
fi

if test "$hpijs_only_build" = "no" && test "$scan_build" = "yes"; then
   AC_CHECK_LIB([sane], [sane_open],, [AC_MSG_ERROR([cannot find sane-backends-devel support (or --disable-scan-build)], 12)])
fi

# AC_DEFINE_DIR([DATADIR], [datadir])
# Copyright © 2006 Stepan Kasal  <kasal@ucw.cz>
# Copyright © 2006 Andreas Schwab <schwab@suse.de>
# Copyright © 2006 Guido U. Draheim <guidod@gmx.de>
# Copyright © 2006 Alexandre Oliva
# Copying and distribution of this file, with or without modification, are permitted in any medium without
# royalty provided the copyright notice and this notice are preserved.
AC_DEFUN([AC_DEFINE_DIR], [
  prefix_NONE=
  exec_prefix_NONE=
  test "x$prefix" = xNONE && prefix_NONE=yes && prefix=$ac_default_prefix
  test "x$exec_prefix" = xNONE && exec_prefix_NONE=yes && exec_prefix=$prefix
dnl In Autoconf 2.60, ${datadir} refers to ${datarootdir}, which in turn
dnl refers to ${prefix}.  Thus we have to use `eval' twice.
  eval ac_define_dir="\"[$]$2\""
  eval ac_define_dir="\"$ac_define_dir\""
  $1="$ac_define_dir"
  test "$prefix_NONE" && prefix=NONE
  test "$exec_prefix_NONE" && exec_prefix=NONE
])

AC_DEFINE_DIR([abs_datadir], [datadir])
AC_DEFINE_DIR([abs_sbindir], [sbindir])
AC_DEFINE_DIR([abs_hpppddir], [hpppddir])
AC_DEFINE_DIR([abs_docdir], [hpdocdir])
abs_ppddir=${abs_hpppddir%/*}
AC_DEFINE_DIR([abs_foomaticdir], [foomaticdir])

AC_SUBST(abs_datadir) 
AC_SUBST(abs_sbindir) 
AC_SUBST(abs_hpppddir) 
AC_SUBST(abs_docdir) 
AC_SUBST(abs_ppddir) 
AC_SUBST(abs_foomaticdir) 
AC_SUBST(icondir)
AC_SUBST(cupsbackenddir)
AC_SUBST(hpppddir)
AC_SUBST(hpdocdir)
AC_SUBST(foomaticdir)
AC_SUBST(network_build)
AC_SUBST(pp_build)
AC_SUBST(gui_build)
AC_SUBST(scan_build)
AC_SUBST(fax_build)
AC_SUBST(cups11_build)
AC_SUBST(foomatic_xml_install)
AC_SUBST(foomatic_ppd_install)
AC_SUBST(doc_build)
AC_SUBST(shadow_build)
AC_SUBST(APDK_ENDIAN_FLAG)
AC_SUBST(APDK_AUTO_INCLUDE_FLAG)

AC_CONFIG_FILES(Makefile hplip.conf hplip.desktop)
AC_OUTPUT
